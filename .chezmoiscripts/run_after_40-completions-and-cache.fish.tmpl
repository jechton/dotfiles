#!/usr/bin/env fish

{{ template "scripts-library-fish" }}

log_task "Setting up fish completions"

mkdir -p $__fish_config_dir/completions

if type -q chezmoi
    log_c "chezmoi completion"
    chezmoi completion fish --output=$__fish_config_dir/completions/chezmoi.fish
end

if type -q bat
    log_c "bat completion"
    curl --silent --show-error "https://raw.githubusercontent.com/sharkdp/bat/master/assets/completions/bat.fish.in" >$__fish_config_dir/completions/bat.fish
end

if type -q gh
    log_c "GitHub completion"
    gh completion -s fish >$__fish_config_dir/completions/gh.fish
end

if type -q chezmoi_modify_manager
    log_c "chezmoi_modify_manager completion"
    chezmoi_modify_manager --bpaf-complete-style-fish >$__fish_config_dir/completions/chezmoi_modify_manager.fish
end

# {{ if .blue -}}
log_c "ujust completion"
ujust --completions fish | sed 's/\bjust\b/ujust/g' | sed s/_just/_ujust/g >$__fish_config_dir/completions/ujust.fish
# {{- end }}

fish_update_completions

for cmd in bat batcat
    if type -q $cmd
        log_task "Building $cmd cache"
        $cmd cache --build
    end
end
